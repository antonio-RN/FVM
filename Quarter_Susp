model Quarter_Susp
  import g = Modelica.Constants.g_n;
  import Modelica.Units.SI.*;
  import source_Force = Modelica.Mechanics.Translational.Sources.Force;
  import Modelica.Mechanics.Translational.Interfaces.Flange_a;
  import Modelica.Mechanics.Translational.Components.Spring;
  import Modelica.Mechanics.Translational.Components.Damper;
  import Modelica.Mechanics.Translational.Components.SpringDamper;
  import Modelica.Mechanics.Translational.Components.Mass;  
  import FVM.comps.Flange_f1_out;
  
  Flange_a chassis_fix "Input flange for chassis fixing point, connected at upper part of spring-damper";
  source_Force unsprung_fz(f = unsprung_mass.m * g) "[N] Force equivalent to unsprung weight";
  Spring spring(c = 45000, s_rel0 = 0.3) "Suspension spring";
  Damper damper(d = 3500) "Suspension damper";
  Flange_a upright "Input flange for anti-roll bar, connected at upper part of unsprung_mass (fz, position)";
  Mass unsprung_mass(m = 25) "Representation of unsprung mass (wheel assembly + half suspension links weight)";
  SpringDamper tire (c = 120000, d = 1000, s_rel0 = 0.1) "Tire rubber equivalent spring-damper system";
  
  Flange_f1_out tire_load "Output flange for vertical load acting on the tire, casual for info tranasfer with wheel model (fz)";
  Flange_a ground "Input flange for ground contact, connected to lower part of tire (fz, position)";
  
equation
//Component connection (spring-damper in parallel -> unsprung_mass -> tire)
//Inputs to the assembly: chassis_fix (upper point spring-damper); upright (upper point of unsprung_mass); ground (lower point of tire)
  connect(chassis_fix, spring.flange_a);
  connect(chassis_fix, damper.flange_a);
  connect(spring.flange_b, unsprung_mass.flange_a);
  connect(damper.flange_b, unsprung_mass.flange_a);
  connect(unsprung_fz.flange, unsprung_mass.flange_a);
  connect(upright, unsprung_mass.flange_a);
  connect(unsprung_mass.flange_b, tire.flange_a);
  connect(tire.flange_b, ground);
  
//Model info transfer
  tire_load.fz = tire.f;
end Quarter_Susp;