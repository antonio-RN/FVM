model Full_Susp
  import FVM.variables.*;
  import Modelica.Mechanics.Translational.Components.Spring;
  import Modelica.Mechanics.Translational.Components.Fixed;
  import FVM.comps.Flange_f1_out;
  import FVM.comps.Flange_a2_in;
  import FVM.comps.Flange_an2_out;
  
  Flange_f1_out FL_tire_load, FR_tire_load, RL_tire_load, RR_tire_load;
  Flange_a2_in CG_acc;
  Flange_an2_out sprung_angles;
  
  //Full suspension assemblies connection: 4x quarter_susp labeled as FL (Front Left), FR (Front Right), RL (Rear Left), RR (Rear Right)
  FVM.Quarter_Susp FL;
  FVM.Quarter_Susp FR;
  FVM.Quarter_Susp RL;
  FVM.Quarter_Susp RR;
  
  //Anti-roll bar conection: 2x linear translational springs labeled as arb_f (Front), arb_r (Rear)
  Spring arb_f;
  Spring arb_r;
  
  //Ground contact points (fixed)
  Fixed ground_FL;
  Fixed ground_FR;
  Fixed ground_RL;
  Fixed ground_RR;
  
  //Sprung body instance
  FVM.Instance_Sprung Sprung;
  
initial equation
//FL
  FL.spring.c = c_f;
  FL.spring.s_rel0 = s_rel0_c_f;
  FL.damper.d = d_f;
  FL.unsprung_mass.m = m_unsprung_f;
  FL.tire.c = c_tire;
  FL.tire.s_rel0 = s_rel0_tire;
  FL.tire.d = d_tire;
  ground_FL.s0 = 0;
  
//FR
  FR.spring.c = c_f;
  FR.spring.s_rel0 = s_rel0_c_f;
  FR.damper.d = d_f;
  FR.unsprung_mass.m = m_unsprung_f;
  FR.tire.c = c_tire;
  FR.tire.s_rel0 = s_rel0_tire;
  FR.tire.d = d_tire;
  ground_FR.s0 = 0;
  
//Front anti-roll bar
  arb_f.c = c_arb_f;
  arb_f.s_rel0 = 0;
  
//RL
  RL.spring.c = c_r;
  RL.spring.s_rel0 = s_rel0_c_r;
  RL.damper.d = d_r;
  RL.unsprung_mass.m = m_unsprung_r;
  RL.tire.c = c_tire;
  RL.tire.s_rel0 = s_rel0_tire;
  RL.tire.d = d_tire;
  ground_RL.s0 = 0;
  
//RR
  RR.spring.c = c_r;
  RR.spring.s_rel0 = s_rel0_c_r;
  RR.damper.d = d_r;
  RR.unsprung_mass.m = m_unsprung_r;
  RR.tire.c = c_tire;
  RR.tire.s_rel0 = s_rel0_tire;
  RR.tire.d = d_tire;
  ground_RR.s0 = 0;
  
//Rear anti-roll bar
  arb_r.c = c_arb_r;
  arb_r.s_rel0 = 0;
  
//Initial conditions
  FL.spring.s_rel = FL.spring.s_rel0;
  FR.spring.s_rel = FR.spring.s_rel0;
  RL.spring.s_rel = RL.spring.s_rel0;
  RR.spring.s_rel = RR.spring.s_rel0;
  FL.tire.s_rel = FL.tire.s_rel0;
  FR.tire.s_rel = FR.tire.s_rel0;
  RL.tire.s_rel = RL.tire.s_rel0;
  RR.tire.s_rel = RR.tire.s_rel0;
  
equation
//Connection of ground contact point per-wheel
  connect(FL.ground, ground_FL.flange);
  connect(FR.ground, ground_FR.flange);
  connect(RL.ground, ground_RL.flange);
  connect(RR.ground, ground_RR.flange);
  
//Connection of anti-roll bar between per-axle uprights
  connect(FL.upright, arb_f.flange_a);
  connect(arb_f.flange_b, FR.upright);
  connect(RL.upright, arb_r.flange_a);
  connect(arb_r.flange_b, RR.upright);
  
//Connection of suspension sub-assemblies to sprung body
  connect(FL.chassis_fix, Sprung.FL_fix);
  connect(FR.chassis_fix, Sprung.FR_fix);
  connect(RL.chassis_fix, Sprung.RL_fix);
  connect(RR.chassis_fix, Sprung.RR_fix);
  
//Model info transfer
  connect(FL_tire_load, FL.tire_load);
  connect(FR_tire_load, FR.tire_load);
  connect(RL_tire_load, RL.tire_load);
  connect(RR_tire_load, RR.tire_load); 
  connect(CG_acc, Sprung.CG_acc);
  connect(Sprung.sprung_angles, sprung_angles);
end Full_Susp;